buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url 'http://artifactory.vokal.io/artifactory/plugins-release'
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }
    }
}

plugins {
    id 'com.jfrog.artifactory' version '3.0.1' 
}

apply plugin: 'com.jfrog.artifactory'
apply plugin: 'groovy'
apply plugin: 'maven-publish'

group = 'io.vokal.gradle'
version = '1.0.1'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.android.tools.build:gradle:2.1.2'
    compile 'org.apache.pdfbox:pdfbox:2.0.0'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            repoKey = 'plugins-release-local'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
//            maven = true
        }
        defaults {
            //This closure defines defaults for all 'artifactoryPublish' tasks of all projects the plugin is applied to
            publications ('mavenJava') //Optional list of publications (names or objects) to publish.
            publishBuildInfo = true   //Publish build-info to Artifactory (true by default)
            publishArtifacts = true   //Publish artifacts to Artifactory (true by default)
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId "resgen"
            artifact sourcesJar
            artifact javadocJar
        }
    }
}
